open import MvuHTML;
open import MvuAttrs;
open import MvuEvents;
open import MvuSubscriptions;
import Mvu;
import MvuCommands;
import WebRTC;

#module JSApp {
#  alien javascript "js/app.js" {
#  }
#}

fun broadcast(clients, msg) server {
    switch (clients) {
        case [] -> ()
        case c::clients ->
            c ! msg;
            broadcast(clients, msg)
    }
}

fun serverLoop(clients) server {
    receive {
        case Register(pid) ->
          var newClients = pid :: clients;
          serverLoop(newClients)
        case BcastModelMsg(msg) ->
          broadcast(clients, ModelMessage(msg));
          serverLoop(clients)
    }
}

var serverPid = spawn { serverLoop([]) };

typename Circle = (id: String, name: String, x : Float, y: Float, vx : Float, vy : Float);

typename CircleState = [| Up | Down | Left | Right | Still |];

typename ServerMessage = Circle;

typename Msg =
  [| NewFrame
   | MoveCircle : CircleState
   | Joined
   | NoOp
   | ServerMsg:ServerMessage
  |];

typename RequestMsg =
  [| ModelMessage:ServerMessage | RetrieveMsg:Process({hear:ServerMessage}) |];

typename Room =
  (circle : Circle, circleState : CircleState, joined : Bool, others : [Circle], pid:Process({hear:RequestMsg}));

var width = 700.0;
var height = 700.0;
var halfWidth = 350.0;
var halfHeight = 350.0;
var circleVelocity = 5.0;

fun defaultRoom(clientPid) {
  (circle = (id = "None", name = "None", x = halfWidth, y = halfHeight, vx = circleVelocity, vy = circleVelocity),
    circleState = Still, joined = false, others = [], pid = clientPid)
}

fun animFrameHandler() {
  animationFrame(fun(_) { NewFrame })
}

var upKey = "ArrowUp";
var downKey = "ArrowDown";
var leftKey = "ArrowLeft";
var rightKey = "ArrowRight";
var spaceKey = " ";


fun keyDownHandler() {
  open MvuSubscriptions;

  onKeyDown(fun(evt) {
    var keycode = evt.key;
    if (keycode == upKey) {
      MoveCircle(Up)
    } else if (keycode == downKey) {
      MoveCircle(Down)
    } else if (keycode == leftKey) {
      MoveCircle(Left)
    } else if (keycode == rightKey) {
      MoveCircle(Right)
    } else if (keycode == spaceKey) {
      Joined
    } else {
      NoOp
    }
  })
}

fun keyUpHandler() {
  open MvuSubscriptions;

  onKeyUp(fun(evt) {
    var keycode = evt.key;
    if (keycode == upKey || keycode == downKey || keycode == leftKey || keycode == rightKey) {
      MoveCircle(Still)
    } else {
      NoOp
    }
  })
}

var ae = MvuAttrs.empty;
var he = MvuHTML.empty;
fun ch(xs) { MvuHTML.concat(xs) }

sig drawCircle : (Circle) -> HTML(Msg)
fun drawCircle(c) {
  var x = c.x;
  var y = c.y;
  var name = c.name;
  div (class ("circle") +@
       style ("left:" ^^ floatToString(x) ^^ "px;top:" ^^ floatToString(y) ^^ "px;position:absolute;"),
       p (class("circleName"), textNode(name))
  )
}

sig listOfHTMLCircles : ([Circle]) ~> [HTML(Msg)]
fun listOfHTMLCircles(cs) {
  switch(cs) {
    case []         -> []
    case circle::xs ->
      drawCircle(circle) :: listOfHTMLCircles(xs)
  }
}

fun moveCircle(circle, direction) {
  if (direction == Up) {
    (circle with y = circle.y -. circleVelocity)
  } else if (direction == Down) {
    (circle with y = circle.y +. circleVelocity)
  } else if (direction == Left) {
    (circle with x = circle.x -. circleVelocity)
  } else if (direction == Right) {
    (circle with x = circle.x +. circleVelocity)
  } else {
    circle
  }
}

fun removeCircle(c, cs) {
  switch(cs) {
    case [] -> []
    case x::xs ->
      if (x.id == c.id)
        removeCircle(c, xs)
      else
        x :: removeCircle(c, xs)
  }
}

sig retrieveServerMessage : (%) {}~> Msg
fun retrieveServerMessage(pid) {
  spawnWait {
    pid ! RetrieveMsg(self());
    receive {
        case msg -> ServerMsg(msg)
    }
  }
}

fun togglePeerCamera(id, mode) {
  var elem = getNodeById("remoteVideo_" ^^ id);
  var vid = getNodeById(id);
  if (isNull(elem) == false) {
    if (mode == "show") {
      domSetAttributeFromRef(elem, "style", "display: block");
      domSetAttributeFromRef(vid, "muted", "false");
      ()
    } else {
      domSetAttributeFromRef(elem, "style", "display: none");
      domSetAttributeFromRef(vid, "muted", "true");
      ()
   }
  } else {
    ()
  }
}

sig closeEnough : (Circle, Circle) ~> ()
fun closeEnough(c1, c2) {
  var distance = sqrt((c2.x -. c1.x)^.2.0 +. (c2.y -. c1.y)^.2.0);
  if (distance <= 100.0) {
    togglePeerCamera(c1.id, "show");
    ()
  } else {
    togglePeerCamera(c1.id, "hide");
    ()
  }
}

fun closeToOthers(c, others) {
  switch(others) {
    case [] -> ()
    case x::xs ->
      closeEnough(x, c);
      closeToOthers(c, xs)
  }
}

# Currently, msg is a circle
sig processServerMsg : (Circle, Room) ~> Room
fun processServerMsg(msg, room) {
    var c = msg;
    if (c.id <> room.circle.id && room.joined == true) {
      closeEnough(c, room.circle);
      var removeOther = removeCircle(c, room.others);
      var addNewOther = c :: removeOther;
      (room with others = addNewOther)
    } else {
      room
    }
}

sig updt : (Msg, Room) ~> (Room, MvuCommands.Command(Msg))
fun updt(msg, room) {
  switch(msg) {
    case NewFrame ->
      var newCircle = moveCircle(room.circle, room.circleState);
      ((room with circle = newCircle, circleState = room.circleState), MvuCommands.empty)
    case MoveCircle(i) ->
      if (room.joined == true) {
        var newCircle = moveCircle(room.circle, i);
        closeToOthers(newCircle, room.others);
        serverPid ! BcastModelMsg(newCircle);
        ((room with circle = newCircle, circleState = i), MvuCommands.empty)
      } else {
        (room, MvuCommands.empty)
      }
    case Joined ->
      var c = (room.circle with id = WebRTC.getUUID(), name = WebRTC.getDisplayName());
      serverPid ! BcastModelMsg(c);
      ((room with circle = c, joined = true), MvuCommands.empty)
    case NoOp -> (room, MvuCommands.empty)
    case ServerMsg(msg) ->
      var room = processServerMsg(msg, room);
      var f = fun() { retrieveServerMessage(room.pid) };
      (room, Spawn(f))
  }
}

sig view : (Room) ~> HTML(Msg)
fun view(room) {
  if (room.joined == true) {
    div (class ("canvas"), ch([
      drawCircle(room.circle),
      ch(listOfHTMLCircles(room.others))
    ]))
  } else {
    div (class ("canvas2"), ch([
      h1 (class ("welcomeText"), textNode("Welcome!")),
      p (class ("instructions"), textNode("Press SPACE to join"))
    ]))
  }
}

sig subscriptions : (Room) -> Sub(Msg)
fun subscriptions(model) {
  animFrameHandler() +^ keyUpHandler() +^ keyDownHandler()
}


fun drain(msgs, pids) {
    switch (msgs) {
        case [] -> (msgs, pids)
        case msg :: msgs ->
            switch (pids) {
                case [] -> (msgs, pids)
                case pid :: pids ->
                    pid ! msg;
                    (msgs, pids)
            }
    }
}

# Invariant: At least one of msgs, pids is empty.
fun clientLoop(msgs, pids) {
    receive {
      case ModelMessage(msg) ->
        var (msgs, pids) = drain(msgs ++ [msg], pids);
        clientLoop(msgs, pids)
      case RetrieveMsg(pid) ->
        var (msgs, pids) = drain(msgs, pids ++ [pid]);
        clientLoop(msgs, pids)
    }
}

fun clientRegister() {
  WebRTC.beginConnection();
  serverPid ! Register(self());
  clientLoop([], [])
}

fun mainPage(serverPid) {
  var clientPid = spawnClient { clientRegister() };
  Mvu.run("placeholder", defaultRoom(clientPid), view,
      updt,
      subscriptions, MvuCommands.spawnProc(fun () { retrieveServerMessage(clientPid) }));
  page
  <html>
    <head>
      <title>Room</title>
      <link rel="stylesheet" href="css/room.css" type="text/css" />
    </head>
    <body>
      <div id="placeholder"></div>
      <div id="localVid"></div>
      <div id="otherVideos"></div>
    </body>
  </html>
}

fun main() {
  addRoute("/", fun(_) { mainPage(serverPid) });
  addStaticRoute("/css", "css", [("css", "text/css")]);
  addStaticRoute("/js", "js", [("js", "text/javascript")]);
  serveWebsockets();
  servePages()
}

main()
